apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.adrian.gomoku"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "default_channel"]
    }
    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file("/home/adrian/Android/appkey.keystore")
            storePassword "lengyuxuan1534."
            keyAlias "adrian_keystore"
            keyPassword "lengyuxuan1534."
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为五子棋_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "五子棋_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {

        default_channel {}
        wandoujia {}
        _360 {}
        yingyongbao {}
        xiaomi {}
        baidu {}
        huawei {}
        jifeng {}
        meizu {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })


    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
    compile 'com.yalantis:contextmenu:1.0.7'
    compile 'com.android.support:support-v4:25.1.0'
    testCompile 'junit:junit:4.12'
    compile 'com.larswerkman:HoloColorPicker:1.5'
}
